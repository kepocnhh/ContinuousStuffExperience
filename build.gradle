buildscript {
    ext {
        jacocoVersion = "0.8.4"
    }
    apply from: 'kotlin.gradle'

    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

repositories {
    mavenCentral()
}

apply from: "util.gradle"
apply plugin: 'jacoco'

evaluationDependsOnChildren()

task collectTestReport(type: TestReport) {
	destinationDir = file("$buildDir/reports/tests")
	testResultDirs = subprojects*.test.binResultsDir
}

task runTests {
	def tasks = subprojects*.test
	def size = tasks.size
	if(size == 0) {
    	println "\tno test tasks"
		return
	}
	for(def i=0; i<size-1; i++) {
		tasks[i].finalizedBy tasks[i+1]
	}
    dependsOn tasks.first()
}


jacoco {
  toolVersion = jacocoVersion
}

def testCoverageReportPath = "$buildDir/reports/coverage"
def testCoverageReportXmlFullPath = "$testCoverageReportPath/xml/report.xml"

task collectTestCoverageReport(type: JacocoReport) {
    def projects = subprojects.findAll {
        it.pluginManager.hasPlugin("jacoco") && it.tasks.find { it.name == "test" } != null
    }
    reports {
        xml.enabled true
        xml.destination file(testCoverageReportXmlFullPath)
        html.enabled true
        html.destination file("$testCoverageReportPath/html")
        csv.enabled false
    }
    getAdditionalSourceDirs().from(files(projects.sourceSets.main.allSource.srcDirs))
    getSourceDirectories().from(files(projects.sourceSets.main.allSource.srcDirs))
    getClassDirectories().from(files(projects.sourceSets.main.output))
    getExecutionData().from(files(projects.jacocoTestReport.executionData))
    doLast {
        def files = []
        file(testCoverageReportPath).eachFileRecurse {
            if(!it.isDirectory())
            if(!it.name.contains("jacoco-sessions"))
            if(it.name.endsWith(".html")) {
                files.add(it)
            }
        }
        def result = files.sort { it.absolutePath }.inject("") { accumulator, file ->
            accumulator + getFileText(file.absolutePath)
        }
        def signatureFile = file("$testCoverageReportPath/signature")
        signatureFile.delete()
        signatureFile << result.digest("SHA-512")
    }
}

task runTestCoverageVerification {
    doLast {
        def result = getTestCoverageResult(testCoverageReportXmlFullPath)
        println "\ttest coverage result: " + (100*result).toLong().toString() + "%"
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

dependencies {
    jacocoAnt "org.jacoco:org.jacoco.ant:$jacocoVersion"
}

task checkReadme() {
    doLast {
        def testCoverageResult = getTestCoverageResult(testCoverageReportXmlFullPath)
        def testCoverageBadgeUrl = createBadgeUrl(
            "test_coverage",
            (100*testCoverageResult).toLong().toString() + "%25",
            getTestCoverageResultBadgeColor(testCoverageResult)
        )
        def readmeFileName = "README.md"
        def readmeText = getFileText(readmeFileName)
        if(readmeText == null || readmeText.isEmpty()) {
            throw new IllegalStateException("File $readmeFileName must not be empty!")
        }
        def hash = getFileText("$testCoverageReportPath/signature")
        if(hash == null || hash.isEmpty()) {
            throw new IllegalStateException("Test coverage signature must not be empty!")
        }
        def testCoverageReportUrl = "https://kepocnhh.github.io/ContinuousStuffExperience/reports/coverage/$hash"
        def testCoverageBadge = "[![test coverage]($testCoverageBadgeUrl)]($testCoverageReportUrl)"
        if(!readmeText.contains(testCoverageBadge)) {
            throw new IllegalStateException(
                "File $readmeFileName must contains test coverage result badge:\n$testCoverageBadge"
            )
        }
    }
}